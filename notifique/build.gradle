// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

kotlin {
  experimental {
    coroutines 'enable'
  }
}

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId 'com.nathanrassi.notifique'

    minSdkVersion 19
    targetSdkVersion 27

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    abortOnError true
    warningsAsErrors true
    lintConfig rootProject.file('lint.xml')
    textReport true
    textOutput 'stdout'
    htmlReport true
    htmlOutput rootProject.file("lint-reports/${name}.html")
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
  }

  productFlavors {
    flavorDimensions 'environment'

    internal {
      applicationIdSuffix 'internal'
      dimension 'environment'
    }

    production {
      dimension 'environment'
    }
  }
}

repositories {
  google()
}

dependencies {
  implementation deps.kotlin.stdlibJre7
  implementation deps.kotlin.coroutines
  implementation deps.kotlin.coroutinesAndroid
  internalImplementation deps.okio
  implementation deps.paging
  implementation deps.supportLibrary.annotations
  implementation deps.supportLibrary.appCompat
  implementation deps.supportLibrary.recyclerView
  implementation deps.room.runtime
  kapt deps.room.compiler
  implementation deps.dagger.runtime
  kapt deps.dagger.compiler
  implementation deps.daggerAndroid.runtime
  kapt deps.daggerAndroid.compiler
  testImplementation deps.junit
  testImplementation deps.truth
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.squareup.sqldelight'

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId 'com.nathanrassi.notifique'

    minSdkVersion 23
    targetSdkVersion 31

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    abortOnError true
    warningsAsErrors true
    lintConfig rootProject.file('lint.xml')
    textReport true
    textOutput 'stdout'
    htmlReport true
    htmlOutput rootProject.file("lint-reports/${name}.html")
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
  }

  buildTypes {
  }

  productFlavors {
    flavorDimensions 'environment'

    internal {
      applicationIdSuffix '.internal'
      dimension 'environment'
    }

    production {
      dimension 'environment'
    }
  }
}

sqldelight {
  Database {
    packageName = "com.nathanrassi.notifique"
  }
}

repositories {
  google()
}

dependencies {
  implementation deps.kotlin.stdlibJdk8
  implementation deps.kotlin.coroutines
  implementation deps.kotlin.coroutinesAndroid
  internalImplementation deps.okio
  internalImplementation deps.leakcanary
  implementation deps.sqldelight.androidDriver
  implementation deps.sqldelight.androidPagingExtensions
  implementation deps.paging
  implementation deps.androidAnnotations
  implementation deps.appcompat
  implementation deps.material
  implementation deps.recyclerview.core
  implementation deps.recyclerview.selection
  implementation deps.dagger.runtime
  kapt deps.dagger.compiler
  implementation deps.daggerAndroid.runtime
  kapt deps.daggerAndroid.compiler
  testImplementation deps.junit
  testImplementation deps.truth
}

buildscript {
  ext.versions = [
      'androidGradlePlugin': '3.0.0-alpha8',
      'buildTools': '26.0.0',
      'compileSdk': 26,
      'errorProne': '2.0.21',
      'errorProneGradlePlugin': '0.0.11',
      'kotlin': '1.1.3-2',
      'supportLibrary': '26.0.0',
      'room': '1.0.0-alpha4',
      'truth': '0.34'
  ]

  ext.deps = [
      'androidGradlePlugin': "com.android.tools.build:gradle:$versions.androidGradlePlugin",
      'errorProneGradlePlugin': "net.ltgt.gradle:gradle-errorprone-plugin:$versions.errorProneGradlePlugin",
      'kotlin': [
          'stdlibJre7':"org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin",
          'gradlePlugin':"org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
      ],
      'supportLibrary': [
          'annotations': "com.android.support:support-annotations:$versions.supportLibrary",
          'appCompat': "com.android.support:appcompat-v7:$versions.supportLibrary"
      ],
      'room': [
          'runtime': "android.arch.persistence.room:runtime:$versions.room",
          'compiler': "android.arch.persistence.room:compiler:$versions.room"
      ],
      'truth': "com.google.truth:truth:$versions.truth"
  ]

  dependencies {
    classpath deps.androidGradlePlugin
    classpath deps.errorProneGradlePlugin
    classpath deps.kotlin.gradlePlugin
  }

  repositories {
    mavenCentral()
    maven { url 'https://maven.google.com/' }
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

allprojects {
  apply plugin: 'net.ltgt.errorprone'
  apply plugin: 'checkstyle'

  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Werror']
  }

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
  }

  checkstyle {
    toolVersion = '7.7'
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone'
            && details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }

  afterEvaluate { project ->
    project.configurations.all {
      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.version.contains('+')) {
          throw new GradleException(
              "Wildcard dependency forbidden: ${requested.group}:${requested.name}:${requested.version}")
        }
      }
    }
  }
}

subprojects {
  afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
  }
}

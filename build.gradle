import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  ext.versions = [
      'androidGradlePlugin': '3.5.3',
      'compileSdk': 29,
      'coroutines': '1.3.3',
      'dagger': '2.26',
      'errorProne': '2.3.4',
      'errorProneGradlePlugin': '1.1.1',
      'junit': '4.13',
      'kotlin': '1.3.61',
      'leakcanary': '2.2',
      'okio': '2.4.3',
      'paging': '2.1.1',
      'sqldelight': '1.2.2',
      'supportLibrary': '1.1.0',
      'truth': '1.0.1'
  ]

  ext.deps = [
      'androidGradlePlugin': "com.android.tools.build:gradle:$versions.androidGradlePlugin",
      'dagger': [
          'compiler': "com.google.dagger:dagger-compiler:$versions.dagger",
          'runtime': "com.google.dagger:dagger:$versions.dagger"
      ],
      'daggerAndroid': [
          'compiler': "com.google.dagger:dagger-android-processor:$versions.dagger",
          'runtime': "com.google.dagger:dagger-android:$versions.dagger"
      ],
      'errorProne': [
          'core': "com.google.errorprone:error_prone_core:$versions.errorProne",
          'gradlePlugin': "net.ltgt.gradle:gradle-errorprone-plugin:$versions.errorProneGradlePlugin"
      ],
      'junit': "junit:junit:$versions.junit",
      'kotlin': [
          'coroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines",
          'coroutinesAndroid': "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines",
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
          'stdlibJdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
      ],
      'leakcanary': "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",
      'okio': "com.squareup.okio:okio:$versions.okio",
      'paging': "androidx.paging:paging-runtime:$versions.paging",
      'sqldelight': [
          'androidDriver': "com.squareup.sqldelight:android-driver:$versions.sqldelight",
          'androidPagingExtensions': "com.squareup.sqldelight:android-paging-extensions:$versions.sqldelight",
          'gradlePlugin': "com.squareup.sqldelight:gradle-plugin:$versions.sqldelight"
      ],
      'supportLibrary': [
          'annotations': "androidx.annotation:annotation:$versions.supportLibrary",
          'appCompat': "androidx.appcompat:appcompat:$versions.supportLibrary",
          'material': "com.google.android.material:material:$versions.supportLibrary",
          'recyclerView': "androidx.recyclerview:recyclerview:$versions.supportLibrary"
      ],
      'truth': "com.google.truth:truth:$versions.truth"
  ]

  dependencies {
    classpath deps.androidGradlePlugin
    classpath deps.errorProne.gradlePlugin
    classpath deps.kotlin.gradlePlugin
    classpath deps.sqldelight.gradlePlugin
  }

  repositories {
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

allprojects {
  apply plugin: 'net.ltgt.errorprone'
  apply plugin: 'checkstyle'

  tasks.withType(JavaCompile) {
    options.errorprone {
      check("DateFormatConstant", CheckSeverity.ERROR)
      check("FloatingPointLiteralPrecision", CheckSeverity.ERROR)
      check("MissingFail", CheckSeverity.ERROR)
      check("MissingOverride", CheckSeverity.ERROR)
      check("OperatorPrecedence", CheckSeverity.ERROR)
      check("ReferenceEquality", CheckSeverity.ERROR)
      check("TypeParameterUnusedInFormals", CheckSeverity.ERROR)
      check("UnnecessaryDefaultInEnumSwitch", CheckSeverity.ERROR)
    }
  }

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
  }

  checkstyle {
    toolVersion = '7.7'
  }

  repositories {
    google()
    mavenCentral()
    jcenter() // Needed for lint.
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone' &&
            details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }

  afterEvaluate { project ->
    project.configurations.all {
      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.version.endsWith('+')) {
          throw new GradleException(
              "Wildcard dependency forbidden: ${requested.group}:${requested.name}:${requested.version}")
        }
      }
    }
  }
}

subprojects {
  afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
  }
}
